// CodeDog List model

requirements = [
    [require, List_implementation]
]

model List<nodeType>{   //model is abstract class
    void: append()
    nodeType: at()
    me void: clear()
    me void: clipToSize()
    itr nodeType: deleteNth()
    itr nodeType: end()
    itr nodeType: erase()
    nodeType: first()
    itr nodeType: front()
    nodeType: __getAt()
    itr nodeType: insert()
    itr nodeType: insertIdx()
    me bool: isEmpty()
    nodeType: last()
    itr nodeType: nthItr()
    nodeType: popFirst()
    nodeType: popLast()
    me void: prepend()
    itr nodeType: __putAt()
    me uint: size()
    me void: pushFirst()
    me void: pushLast()
    me void: begin()
    me void: rEnd()
    nodeType: back()
}

model MapNode<keyType, valueType>{
    keyType: key
    valueType: value
    their RBNode<keyType, valueType>: goNext()
    their RBNode<keyType, valueType>: goPrev()
}
model MapItr<keyType, valueType>{
    our MapNode<keyType, valueType>: node
    our MapNode<keyType, valueType>: goNext()
    //our MapNode<keyType, valueType>: goPrev()
    valueType: val()
}
model Map<keyType, valueType>{
    valueType: at(keyType)
    void: clear()
    me bool: containsKey(keyType: key)
    our MapItr: end()
    void: erase(keyType: key)
    our MapItr: find(keyType: key)
    valueType: first()
    our MapItr: front()
    our MapItr: insert(keyType: key, valueType: value)
    me bool:  isEmpty()
    valueType: last()
    //itr valueType: lower_bound(keyType: key)
    me uint:  size()
}
model MultimapNode<keyType, valueType>{
    keyType: key
    valueType: value
    their RBNode<keyType, valueType>: goNext()
    their RBNode<keyType, valueType>: goPrev()
}
model MultimapItr<keyType, valueType>{
    our MultimapNode<keyType, valueType>: node
    our MultimapNode<keyType, valueType>: goNext()
    //our MultimapNode<keyType, valueType>: goPrev()
    valueType: val()
}
model Multimap<keyType, valueType>{
    valueType: at(keyType)
    void: clear()
    me bool: containsKey(keyType: key)
    our MultimapItr: end()
    void: erase(keyType: key)
    our MultimapItr: find(keyType: key)
    valueType: first()
    our MultimapItr: front()
    our MultimapItr: insert(keyType: key, valueType: value)
    me bool:  isEmpty()
    valueType: last()
    //itr valueType: lower_bound(keyType: key)
    me uint:  size()
}

////////////////////////////////////////////////////////
/*
struct CDListItr<nodeType>{
    nodeType: item
    our CDListItr<nodeType>: next
    our CDListItr<nodeType>: prev
    ///////// CONSTRUCTORS ///////////////////////////
    none: CDListItr(nodeType: value, our CDListItr<nodeType>: _next, our CDListItr<nodeType>: _prev) <- {
        item <- value
        next <- _next
        prev <- _prev
    }
}
struct CDList<nodeType>: implements=List native=codeDog specs={append=constant rangeIteration=constant}{
    our CDListItr<nodeType>: head
    our CDListItr<nodeType>: tail
    me int64: __size

    void: append(nodeType: value) <- {
        our CDListItr<nodeType>:: newNode{value, NULL, tail}
        if(tail!=NULL){tail.next <- newNode}
        if(head==NULL){head <- newNode}
        tail <- newNode
        __size <+- 1
    }
    nodeType: at(me int64: pos) <- {
        if(head == NULL){logFatalError("List access out of range in List::at().")}
        our CDListItr<nodeType>: retVal <- head
        withEach x in RANGE(0..pos){
            retVal <- retVal.next
            if (retVal == NULL){logFatalError("List access out of range in List::at().")}
        }
        return(retVal.item)
    }
    void: clear() <- {
        head<-NULL
        tail<-NULL
        __size <- 0
    }
    void: clipToSize(me int64: size) <- {
        tail <- nthItr(size-1)
        __size <- size
    }
    our CDListItr<nodeType>: deleteNth(me int: pos) <- {return(erase(nthItr(pos)))}
    our CDListItr<nodeType>: end()    <- {return(tail)}
    our CDListItr<nodeType>: erase(our CDListItr<nodeType>: toDel) <- {
        our CDListItr<nodeType>: retVal <- toDel.prev
        if(toDel===head){head<-head.next}
        if(toDel===tail){tail<-tail.prev}
        if(toDel.prev){toDel.prev.next <- toDel.next}
        if(toDel.next){toDel.next.prev <- toDel.prev}
        return(retVal)
    }
    nodeType: first()                 <- {return(head.item)}
    our CDListItr<nodeType>: front()  <- {return(head)}
    //nodeType: __getAt(me int: pos)  <- {}
    our CDListItr<nodeType>: insert(our CDListItr<nodeType>: pos, nodeType: value) <- {
        if(pos==NULL){
            prepend(value)
            return(head)
        }
        our CDListItr<nodeType>: nextNode <- pos.next
        our CDListItr<nodeType>:: newNode {value, nextNode, pos}
        if(nextNode!=NULL){nextNode.prev <- newNode}
        pos.next <- newNode
        if(tail === pos){tail <- newNode}
        return(newNode)
    }
    our CDListItr<nodeType>: insertIdx(me int: pos, nodeType: value) <- {
        if(pos==0){prepend(value);return(head)}
        our CDListItr<nodeType>: item <- nthItr(pos-1)
        insert(item, value)
        return(item)
    }
    me bool: isEmpty()                <- {return(head==NULL)}
    nodeType: last()                  <- {return(tail.item)}
    our CDListItr<nodeType>: nthItr(me int64: pos) <- {
        if(head == NULL){return(NULL)}
        our CDListItr<nodeType>: retVal <- head
        withEach x in RANGE(0..pos){
            retVal <- retVal.next
            if (retVal == NULL){return(NULL)}
        }
        return(retVal)
    }
    nodeType: popFirst() <- {
        me nodeType: retVal
        retVal <- head.item
        if(head!=NULL){
            __size <-- 1
            head <- head.next
            if(head==NULL){tail <- NULL}
        }
        return(retVal)
    }
    nodeType: popLast() <- {
        me nodeType: retVal
        retVal <- tail.item
        if(tail!=NULL){
            __size <-- 1
            tail <- tail.prev
            if(tail==NULL){head <- NULL}
            else{tail.next <- NULL}
        }
        return(retVal)
    }
    me void: prepend(nodeType: value) <- {
        our CDListItr<nodeType>:: newNode {value, head, NULL}
        if(head!=NULL){head.prev <- newNode}
        if(tail==NULL) {tail <- newNode}
        head <- newNode
        __size <+- 1
    }
    //our CDListItr<nodeType>: __putAt(our CDListItr<nodeType>: pos, nodeType: value) <- {}
    me int64: size()                   <- {return(__size)}
    //our CDListItr<nodeType>: back() <- {}
    ///////// CONSTRUCTORS ///////////////////////////
    none: CDList(List<nodeType>: initList) <- {

    }
}
*/
struct RBNode<keyType, valueType>: implements=MapNode{
    keyType: key
    valueType: value
    me bool: isBlack
    me bool: isLeaf
    their RBNode<keyType, valueType>: left
    their RBNode<keyType, valueType>: right
    their RBNode<keyType, valueType>: parent
    // CONSTRUCTORS
    none: RBNode(keyType: _key, valueType: _val) <-{
        key   <- _key
        value <- _val
    }
}

struct RBTreeItr<keyType, valueType>: implements=MapItr{
    their RBNode<keyType, valueType>: node
    their RBTreeMap<keyType, valueType>: container
    their RBNode<keyType, valueType>: goNext() <- {
        if(node.right != NULL and !node.right.isLeaf){
            node <- findSmallest(node.right)
            if(node==NULL or node.isLeaf){node <- container.edge}
            return(node)
        }
        their RBNode<keyType, valueType>: parentTemp <- node.parent
        their RBNode<keyType, valueType>: nodeTemp   <- node
        while(parentTemp!=NULL and nodeTemp===parentTemp.right){
            nodeTemp   <- parentTemp
            parentTemp <- parentTemp.parent
        }
        node <- parentTemp
        if(node==NULL or node.isLeaf){node <- container.edge}
        return(node)
    }
    their RBNode<keyType, valueType>: findSmallest(their RBNode<keyType, valueType>: crntNode) <- {
        their RBNode<keyType, valueType>: prevNode <- NULL
        while(!(crntNode == NULL or crntNode.isLeaf)){
            prevNode <- crntNode
            crntNode <- crntNode.left
        }
        if(prevNode != NULL){return(prevNode)}
        return(crntNode)
    }
    their RBTreeItr<keyType, valueType>: __opAssign(their RBTreeItr<keyType, valueType>: RHS) <- {
        node <- RHS.node
        return(self)
    }
    their RBTreeItr<keyType, valueType>: __inc() <- { // Prefix ++ overload
        if(node){
            node <- goNext()
        }
        return(self)
    }
    me bool: __notEqual(me RBTreeItr<keyType, valueType>: RHS) <- {
        return(node !== RHS.node)
    }
    valueType: __derefPtr() <- {return(node.value)}
    me bool: __isEqual(me RBTreeItr<keyType, valueType>: RHS) <- {
        return(node === RHS.node)
    }
    valueType: val()                         <- <%!%0%>
}

struct RBTreeMap<keyType, valueType>: implements=Map native=codeDog specs={insert=logarithmic find=logarithmic at=logarithmic}{// find=logarithmic at=logarithmic}{  // constant
    their RBNode<keyType, valueType>: Root
    their RBNode<keyType, valueType>: Leaf
    their RBNode<keyType, valueType>: edge
    me uint: mapSize

    valueType: at(keyType: searchKey) <- {
        their RBNode<keyType, valueType>: node
        node <- getNode(Root, searchKey)
        return(node.value)
    }
    void: clear() <- {
        Root <- NULL
        Leaf <- NULL
        mapSize <- 0
    }
    me bool: containsKey(keyType: key) <- {
        if(getNode(Root, key)){return(true)}
        else{return(false)}
    }
    me RBTreeItr<keyType, valueType>: end() <- {
        me RBTreeItr<keyType, valueType>: retItr; retItr.node <- edge
        return(retItr)
    }
    me void: erase(keyType: key) <- {
        their RBNode<keyType, valueType>: node
        node <- getNode(Root, key)
        removeHelper(node, key)
    }
    me RBTreeItr<keyType, valueType>: find(keyType: key) <- {
        their RBNode<keyType, valueType>: retNode <- getNode(Root, key)
        if(retNode==NULL){retNode <- edge}
        me RBTreeItr<keyType, valueType>: retItr; retItr.node <- retNode
        return(retItr)
    }
    valueType: first() <- {
        their RBNode<keyType, valueType>: smallestNode <- findSmallest(Root)
        return(smallestNode.value)
    }
    me RBTreeItr<keyType, valueType>: front() <- {
        me RBTreeItr<keyType, valueType>: retItr; retItr.node <- findSmallest(Root)
        retItr.container <- self
        return(retItr)
    }
    their RBNode<keyType, valueType>: insert(keyType: key, valueType: value) <- {
        their RBNode<keyType, valueType>: newNode
        if(Root == NULL){
            Allocate(Leaf)
            Allocate(edge)
            Leaf.isBlack <-true
            Leaf.isLeaf  <-true
            Allocate(newNode, key, value)
            newNode.isBlack <-true
            newNode.left <- Leaf
            newNode.right <- Leaf
            Root <- newNode
            mapSize <- mapSize + 1
            return(newNode)
        }
        Allocate(newNode, key, value)
        newNode.left <- Leaf
        newNode.right <- Leaf
        BSTInsert(Root, newNode)
        mapSize <- mapSize + 1
        checkColor(newNode)
        return(newNode)
    }
    me bool: isEmpty() <- {
        if(Root==NULL){return(true)}
        return(false)
    }
    valueType: last() <- {
        their RBNode<keyType, valueType>: crntNode <- Root
        their RBNode<keyType, valueType>: prevNode
        while(!(crntNode === Leaf)){
            prevNode <- crntNode
            crntNode <- crntNode.right
        }
        return(prevNode.value)
    }
    //itr valueType: lower_bound(keyType: key) <- {}
    me uint:  size() <- {return(mapSize)}
    //valueType: __index(keyType: searchKey) <- {
        //me RBTreeItr<keyType, valueType>: retItr <- find(searchKey)
        //if(retItr.node == edge){
            //me valueType: newVal
            //retItr.node <- insert(searchKey, newVal)
        //}
        //return(retItr.node.value)
    //}
    ////////////////////////////////////////////////////////////////////
    me bool: isLeftChild(their RBNode<keyType, valueType>: node) <- {
        if(node.parent != NULL and node === node.parent.left){return(true)}
        {return(false)}
    }

    me void: leftRotate(their RBNode<keyType, valueType>: node, me bool: changeColor) <- {
        their RBNode<keyType, valueType>: temp <- node.right
        node.right <- temp.left
        node.right.parent <- node
        if(node.parent == NULL){
            Root <- temp
            temp.parent <- NULL
        }else{
            temp.parent <- node.parent
            if(isLeftChild(node)){temp.parent.left <- temp}
            else{temp.parent.right <- temp}
        }
        temp.left <- node
        node.parent <- temp
        if(changeColor == true){
            node.isBlack <- true
            node.parent.isBlack <- false
        }
    }

    me void: rightRotate(their RBNode<keyType, valueType>: node, me bool: changeColor) <- {
        their RBNode<keyType, valueType>: temp <- node.left
        node.left <- temp.right
        node.left.parent <- node
        if(node.parent == NULL){
            Root <- temp
            temp.parent <- NULL
        }else{
            temp.parent <- node.parent
            if(isLeftChild(node) == false){temp.parent.right <- temp}
            else{temp.parent.left <- temp}
        }
        temp.right <- node
        node.parent <- temp
        if(changeColor == true){
            node.isBlack <- true
            node.parent.isBlack <- false
        }
    }

    me void: rotate(their RBNode<keyType, valueType>: node)<-{
        if(isLeftChild(node) == true){
            if(isLeftChild(node.parent) == true){
            rightRotate(node.parent.parent, false)
            node.isBlack <-false
            node.parent.isBlack <- true
            node.parent.right.isBlack <- false
            return()
            }
            rightRotate(node.parent, false)
            leftRotate(node.parent.parent, false)
            node.isBlack <-true
            node.right.isBlack <- false
            node.left.isBlack <- false
            return()
        }
        //// rotate for right child
        if(isLeftChild(node.parent) == false){
            leftRotate(node.parent.parent, false)
            node.isBlack <-false
            node.parent.isBlack <- true
            node.parent.left.isBlack <- false
            return()
        }
        leftRotate(node.parent, false)
        rightRotate(node.parent.parent, false)
        node.isBlack <-true
        node.right.isBlack <- false
        node.left.isBlack <- false
        return()
    }

    me void: correctTree(their RBNode<keyType, valueType>: node)<-{
        if(node.parent===Root){
            print ("ERROR: PARENT ", node.parent.key," is Root.  Node key is ", node.key, "\n")
            exit(1)
        }
        //// PARENT IS LEFT CHILD
        if(isLeftChild(node.parent) == true){
            //// BLACK AUNT ROTATE
            if(node.parent.parent.right.isBlack == true){
                rotate(node)
                return()
            }
            //// RED AUNT COLOR FLIP
            node.parent.parent.right.isBlack <- true
            node.parent.parent.isBlack <- false
            node.parent.isBlack <- true
            return()
        }
        //// PARENT IS RIGHT CHILD
        //// BLACK AUNT ROTATE
        if(node.parent.parent.left.isBlack == true){
            rotate(node)
            return()
        }
        //// RED AUNT COLOR FLIP
        node.parent.parent.left.isBlack <- true
        node.parent.parent.isBlack <- false
        node.parent.isBlack <- true
        return()
    }

    me void: checkColor(their RBNode<keyType, valueType>: node) <-{
        if(node === Root or node === Leaf){
            Root.isBlack <- true
            return()
        }
        if(node.isBlack == false and node.parent.isBlack == false){correctTree(node)}
        checkColor(node.parent)
    }

    me void: BSTInsert(their RBNode<keyType, valueType>: root, their RBNode<keyType, valueType>: newNode) <- {
        if(newNode.key > root.key){
            if(root.right === Leaf){
                root.right <- newNode
                newNode.parent <- root
                return()
            }
            BSTInsert(root.right, newNode)
            return()
        }
        if(root.left === Leaf){
            root.left <- newNode
            newNode.parent <-root
            return()
        }
        BSTInsert(root.left, newNode)
        return()
    }

    me int: height()<-{
        if(Root == NULL){return(0)}
        return(heightHelper(Root)-1)
    }

    me int: heightHelper(their RBNode<keyType, valueType>: node)<-{
        if(node == NULL){return(0)}
        me int: leftHeight <- heightHelper(node.left)+1
        me int: rightHeight <- heightHelper(node.right)+1
        if(leftHeight > rightHeight){return(leftHeight)}
        return(rightHeight)
    }

    me void: removeHelper(their RBNode<keyType, valueType>: node, keyType: key) <- {
        if(node === Leaf or node == NULL){return()}
        if(node.key == key){
            if(node.right === Leaf or node.left === Leaf){deleteOneChild(node)}
            else{
                their RBNode<keyType, valueType>: inorderSuccessor <- findSmallest(node.right)
                node.key <- inorderSuccessor.key
                removeHelper(node.right, node.key)
            }
        }
        if(node.key < key){removeHelper(node.right, key)}
        else{removeHelper(node.left, key)}
    }

    me void: deleteOneChild(their RBNode<keyType, valueType>: nodeToBeDelete) <- {
        their RBNode<keyType, valueType>: child
        if(nodeToBeDelete.right === Leaf){child <- nodeToBeDelete.left}
        else{child <- nodeToBeDelete.right}
        replaceNode(nodeToBeDelete, child)
        if(nodeToBeDelete.isBlack == true){
            if(child.isBlack == false){child.isBlack <- true}
            else{deleteCase1(child)}
        }
    }

    me void: replaceNode(their RBNode<keyType, valueType>: node, their RBNode<keyType, valueType>: child) <- {
        child.parent <- node.parent
        if(node.parent == NULL){Root <-child}
        else{
            if(isLeftChild(node)){node.parent.left <- child}
            else{node.parent.right <- child}
        }
    }

    their RBNode<keyType, valueType>: findSmallest(their RBNode<keyType, valueType>: crntNode) <- {
        their RBNode<keyType, valueType>: prevNode <- NULL
        while(!(crntNode == NULL or crntNode === Leaf)){
            prevNode <- crntNode
            crntNode <- crntNode.left
        }
        if(prevNode != NULL){return(prevNode)}
        return(crntNode)
    }

    me void: deleteCase1(their RBNode<keyType, valueType>: doubleBlackNode) <- {
        print ("deleteCase1: ", doubleBlackNode.key, "\n")
        if(doubleBlackNode === Leaf){print("    Leaf\n")}
        if(doubleBlackNode.parent == NULL){
            Root <- doubleBlackNode
            return()
        }
        deleteCase2(doubleBlackNode)
    }

    me void: deleteCase2(their RBNode<keyType, valueType>: doubleBlackNode) <- {
        print ("deleteCase2: ", doubleBlackNode.key, "\n")
        their RBNode<keyType, valueType>: siblingNode
        siblingNode <- findSiblingNode(doubleBlackNode)
        if(siblingNode.isBlack == false){
            if(isLeftChild(siblingNode)){rightRotate(siblingNode, true)}
        else{leftRotate(siblingNode, true)}
            if(siblingNode.parent == NULL){Root <- siblingNode}
        }
        deleteCase3(doubleBlackNode)
    }

    me void: deleteCase3(their RBNode<keyType, valueType>: doubleBlackNode) <- {
        print ("deleteCase3: ", doubleBlackNode.key, "\n")
        their RBNode<keyType, valueType>: siblingNode <- findSiblingNode(doubleBlackNode)
        if(doubleBlackNode.parent.isBlack == true and siblingNode.isBlack == true and siblingNode.left.isBlack == true and siblingNode.right.isBlack == true){
            siblingNode.isBlack <- false
            deleteCase1(doubleBlackNode.parent)
        }else{deleteCase4(doubleBlackNode)}
    }

    me void: deleteCase4(their RBNode<keyType, valueType>: doubleBlackNode) <- {
        print ("deleteCase4: ", doubleBlackNode.key, "\n")
        their RBNode<keyType, valueType>: siblingNode <- findSiblingNode(doubleBlackNode)
        if(doubleBlackNode.parent.isBlack == false and siblingNode.isBlack == true and siblingNode.left.isBlack == true and siblingNode.right.isBlack == true){
            siblingNode.isBlack <- false
            doubleBlackNode.parent.isBlack <- true
            return()
        }else{deleteCase5(doubleBlackNode)}
    }

    me void: deleteCase5(their RBNode<keyType, valueType>: doubleBlackNode) <- {
        print ("deleteCase5: ", doubleBlackNode.key, "\n")
        their RBNode<keyType, valueType>: siblingNode <- findSiblingNode(doubleBlackNode)
        if(siblingNode.isBlack == true){
            if(isLeftChild(doubleBlackNode) == true and siblingNode.right.isBlack == true and siblingNode.left.isBlack == false){
                rightRotate(siblingNode.left, true)
            }else if(isLeftChild(doubleBlackNode) == false and siblingNode.left.isBlack == true and siblingNode.right.isBlack == false){
                leftRotate(siblingNode.right, true)
            }
        }
        deleteCase6(doubleBlackNode)
    }

    me void: deleteCase6(their RBNode<keyType, valueType>: doubleBlackNode) <- {
        print ("deleteCase6: ", doubleBlackNode.key, "\n")
        their RBNode<keyType, valueType>: siblingNode <- findSiblingNode(doubleBlackNode)
        siblingNode.isBlack <- siblingNode.parent.isBlack
        siblingNode.parent.isBlack <- true
        if(isLeftChild(doubleBlackNode) == true){
            siblingNode.right.isBlack <- true
            leftRotate(siblingNode, false)
        }else{
            siblingNode.left.isBlack <- true
            rightRotate(siblingNode, false)
        }
        if(siblingNode.parent == NULL){Root <- siblingNode}
    }

    their RBNode<keyType, valueType>: findSiblingNode(their RBNode<keyType, valueType>: node) <- {
        their RBNode<keyType, valueType>: parent<- node.parent
        if(isLeftChild(node) == true){return(parent.right)}
        else{return(parent.left)}
    }

    their RBNode<keyType, valueType>: getNode(their RBNode<keyType, valueType>: node, keyType: searchKey) <- {
        while(!(node === Leaf)){
            if(searchKey < node.key){node <- node.left}
            else if(searchKey > node.key){node <- node.right}
            else if(searchKey == node.key){return(node)}
        }
        return(NULL)
    }

    me void: traverse() <- {
        print("_____________________________________\n")
        print("height: ", height(), "\n")
        print("T R A V E R S E\n")
        traverseHelper(Root, "")
        print("_____________________________________\n")
    }

    me void: traverseHelper(their RBNode<keyType, valueType>: node, me string: indent) <- {
        me string: isBlackStr
        if(node === Leaf){return()}
        traverseHelper(node.left, indent+"    ")
        if(node.isBlack == true){isBlackStr<-"B"}
        else{isBlackStr<-"R"}
        print(indent, node.key, ":", isBlackStr, "\n")
        traverseHelper(node.right, indent+"    ")
    }

    me int: blackNodes(their RBNode<keyType, valueType>: node) <- {
        if(node === Root){return(1)}
        me int: rightBlackNodes <- blackNodes(node.right)
        me int: leftBlackNodes <- blackNodes(node.left)
        if(rightBlackNodes!=leftBlackNodes){print("ERROR blackNodes: ", rightBlackNodes, ", ", leftBlackNodes)}
        if(node.isBlack){leftBlackNodes <- leftBlackNodes + 1}
        return(leftBlackNodes)
    }

}

