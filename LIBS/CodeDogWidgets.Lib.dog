/- CodeDog IntDraggerWidget
featuresNeeded = [GUI_ToolKit]
struct LabelDash: inherits=dash{
    me string:          label
    void: initLabelDash(me GUI_Scalar: PosX, me GUI_Scalar: PosY, me string: Label) <- {
        title                <- "LabelDash."+Label
        label                <- Label
        posX                 <- PosX
        posY                 <- PosY    
    }
    void: draw(me GUI_ctxt: cr) <- {
        me GUI_Scalar: padding <- 5
        cr.setColor(Styler.Black)
        cr.setLineWidth(convertToFloatVal(0.5))
        me deltaSize: size <- getTextSize(cr, label, styler.fontDefault)
        me GUI_Scalar: rightJustifyOffset <- size.width + padding
        renderText(cr, label, styler.fontDefault, posX-rightJustifyOffset, posY+padding+size.height)
    }
}
struct IntDraggerDash: inherits=dash{
    me circleButton: leftIncrBtn
    me circleButton: rightDecrBtn
    me GUI_Scalar:   radius
    me int:          intData
    me GUI_Scalar:   btnSpan
    void: initIntDraggerDash(me GUI_Scalar: PosX, me GUI_Scalar: PosY, me GUI_Scalar: Radius, me string: Label) <- {
        title                <- "IntDraggerDash."+Label
        posX                 <- PosX
        posY                 <- PosY
        radius               <- Radius
        btnSpan              <- 100
        intData              <- 60    
    }
    void: requestRedraw() <- {
        if(dashParent==NULL){log("In IntDraggerDash.requestRedraw() dashParent was NULL")}
        isDirty<-true; 
        dashParent.requestRedraw()
    }
    void: draw(me GUI_ctxt: cr) <- {
        me GUI_Scalar:   padding <- 5
        /- BACKGROUND
        me cdColor: bkCol <- styler.White
        cr.setColor(bkCol)
        cr.paintNow()
        /- FOREGROUND
        cr.setColor(Styler.Black)
        leftIncrBtn.initCircleBtn(posX+padding, posY+padding, radius, "<")
        leftIncrBtn.drawCircleBtn(cr)
        cr.setLineWidth(convertToFloatVal(0.5))
        me string: dataStr <- toString(intData)
        me deltaSize: size <- getTextSize(cr, dataStr, styler.fontDefault)
        renderText(cr, dataStr, styler.fontDefault, posX+padding+radius*2+(btnSpan-size.width)/2, posY+padding+radius+size.height/2)
        rightDecrBtn.initCircleBtn(posX+padding+radius*2+btnSpan, posY+padding, radius, ">")
        rightDecrBtn.drawCircleBtn(cr)
    }
    void: incrValue() <- {
        intData <- intData + 1
    }
    void: decrValue() <- {
        intData <- intData - 1
    }
    void: applyDelta(me int: val) <- {
        intData <- intData + val
    }
}
struct IntDraggerWidget: inherits=DashboardWidget{
    our LabelDash:       labelDash
    our IntDraggerDash:  intDraggerDash
    me int:              lowVal
    me int:              highVal
    me GUI_Scalar:       prevX
    me GUI_Scalar:       deltaX <- 0
    me int:              moveThreshold <- 2
    me bool:             isBtnDown     <- false
    me bool:             isDragging    <- false
    their GUI_canvas: init(me string: label)      <- {
        /-/// CANVAS AND EVENTS
        title                   <- 'IntDraggerWidget.'+label
        me int: radius          <- 20
        dashboardDBW.init(title)
        canvas                  <- thisApp.gui.newCanvas()
        canvas.setParent(self)
        dashboardDBW.dashParent <- self
        dashboardDBW.canvas     <- canvas
        thisApp.gui.setWidgetSize(canvas,320, 50)
        managedWidget           <- canvas
        EventDispatcher.registerWidgetForEventDispatcher(canvas, self)
        /-/// CHILDREN
        intDraggerDash.initIntDraggerDash(120,0,radius,label)
        dashboardDBW.addChild(intDraggerDash)
        labelDash.initLabelDash(120,6,label)
        dashboardDBW.addChild(labelDash)
        return(managedWidget)
    }
    void: draw(me GUI_ctxt: cr) <- {
        dashboardDBW.draw(cr)
    }
    void: INIT()                <- {
        Allocate(dashboardDBW)
        Allocate(labelDash)
        Allocate(intDraggerDash)
    }
    void: requestRedraw() <- {
        markDirtyArea(canvas, 0,0, widgetWidth(canvas), widgetHeight(canvas))
    }
    me bool: primaryUp(their GUI_ButtonEvent: event) <- {
        if (intDraggerDash.rightDecrBtn.primaryUp(event)) {intDraggerDash.incrValue();requestRedraw()}
        else if (intDraggerDash.leftIncrBtn.primaryUp(event)) {intDraggerDash.decrValue();requestRedraw()}
        isBtnDown     <- false
        isDragging    <- false
        deltaX        <- 0
        return(true)
    }
    me bool: primaryDn(their GUI_ButtonEvent: event) <- {
        isBtnDown     <- true
        return(true)
    }
    me bool: mouseMoved(their GUI_MotionEvent: event) <- {
        deltaX <- event.x - prevX
        prevX  <- event.x
        me int: multiplier <- convertToIntVal(deltaX)
        if (isBtnDown and abs(deltaX) > moveThreshold) {
            isDragging    <- true
            intDraggerDash.applyDelta(multiplier)
            requestRedraw()
        }
        return(true)
    }
    me int: getValue() <- {return(intDraggerDash.intData)}
    me void: setValue(me int: val) <- {}
}
struct TimeDraggerDash: inherits=dash{
    me circleButton: leftIncrBtn
    me circleButton: rightDecrBtn
    me GUI_Scalar:   radius
    me timeValue:    timeData
    me int:          minuteOffset
    me GUI_Scalar:   btnSpan
    void: initTimeDraggerDash(me GUI_Scalar: PosX, me GUI_Scalar: PosY, me GUI_Scalar: Radius, me string: Label) <- {
        title                <- "timeDraggerDash."+Label
        posX                 <- PosX
        posY                 <- PosY
        radius               <- Radius
        btnSpan              <- 100    
        minuteOffset         <- 0
	timeData             <- getCurrentTimeStamp()
    }
    void: requestRedraw() <- {
        if(dashParent==NULL){log("In TimeDraggerDash.requestRedraw() dashParent was NULL")}
        isDirty<-true; 
        dashParent.requestRedraw()
    }
    void: draw(me GUI_ctxt: cr) <- {
        me GUI_Scalar:   padding <- 5
        me string:         hrStr <- "10"
        me string:        minStr <- "20"
        me string:       dataStr <- toString(minuteOffset)
        me GUI_Scalar:   YOffset <- posY+padding
        /- BACKGROUND
        me cdColor: bkCol <- styler.White
        cr.setColor(bkCol)
        cr.paintNow()
        /- FOREGROUND
        cr.setColor(Styler.Black)
        leftIncrBtn.initCircleBtn(posX+padding, posY+padding, radius, "<")
        leftIncrBtn.drawCircleBtn(cr)
        cr.setLineWidth(convertToFloatVal(0.5))
        me deltaSize: size <- getTextSize(cr, dataStr, styler.fontDefault)
        YOffset <- YOffset+size.height+padding
        renderText(cr, dataStr, styler.fontDefault, posX+padding+radius*2+(btnSpan-size.width)/2, YOffset)
        dataStr <- unicodeMgr.customPatternDateTime(timeData,"h:mm a","en")
        size <- getTextSize(cr, dataStr, styler.fontSmall)
        YOffset <- YOffset+size.height+padding
        renderText(cr, dataStr, styler.fontSmall, posX+padding+radius*2+(btnSpan-size.width)/2, YOffset)
        rightDecrBtn.initCircleBtn(posX+padding+radius*2+btnSpan, posY+padding, radius, ">")
        rightDecrBtn.drawCircleBtn(cr)
    }
    void: incrValue() <- {
       minuteOffset <- minuteOffset + 1
       timeData     <- timeData + 60000
    }
    void: decrValue() <- {
        minuteOffset <- minuteOffset - 1
	timeData     <- timeData - 60000
    }
    void: applyDelta(me int: val) <- {
        minuteOffset <- minuteOffset + val
	timeData     <- timeData + val * 60000
    }
}
struct TimeDraggerWidget: inherits=DashboardWidget{
    our LabelDash:       labelDash
    our TimeDraggerDash: timeDraggerDash
    me int:              lowVal
    me int:              highVal
    me GUI_Scalar:       prevX
    me GUI_Scalar:       deltaX <- 0
    me int:              moveThreshold <- 2
    me bool:             isBtnDown     <- false
    me bool:             isDragging    <- false
    their GUI_canvas: init(me string: label)      <- {
        /-/// CANVAS AND EVENTS
        title                   <- 'TimeDraggerWidget.'+label
        me int: radius          <- 20
        dashboardDBW.init(title)
        canvas                  <- thisApp.gui.newCanvas()
        canvas.setParent(self)
        dashboardDBW.dashParent <- self
        dashboardDBW.canvas     <- canvas
        thisApp.gui.setWidgetSize(canvas, 320, 65)
        managedWidget           <- canvas
        EventDispatcher.registerWidgetForEventDispatcher(canvas, self)
        /-/// CHILDREN
        timeDraggerDash.initTimeDraggerDash(120,0,radius,label)
        dashboardDBW.addChild(timeDraggerDash)
        labelDash.initLabelDash(120,15,label)
        dashboardDBW.addChild(labelDash)
        return(managedWidget)
    }
    void: draw(me GUI_ctxt: cr) <- {
        dashboardDBW.draw(cr)
    }
    void: INIT()                <- {
        Allocate(dashboardDBW)
        Allocate(labelDash)
        Allocate(timeDraggerDash)
    }
    void: requestRedraw() <- {
        markDirtyArea(canvas, 0,0, widgetWidth(canvas), widgetHeight(canvas))
    }
    me bool: primaryUp(their GUI_ButtonEvent: event) <- {
        if (timeDraggerDash.rightDecrBtn.primaryUp(event)) {timeDraggerDash.incrValue();requestRedraw()}
        else if (timeDraggerDash.leftIncrBtn.primaryUp(event)) {timeDraggerDash.decrValue();requestRedraw()}
        isBtnDown     <- false
        isDragging    <- false
        deltaX        <- 0
        return(true)
    }
    me bool: primaryDn(their GUI_ButtonEvent: event) <- {
        isBtnDown     <- true
        return(true)
    }
    me bool: mouseMoved(their GUI_MotionEvent: event) <- {
        deltaX <- event.x - prevX
        prevX  <- event.x
        me int: multiplier <- convertToIntVal(deltaX)
        if (isBtnDown and abs(deltaX) > moveThreshold) {
            isDragging    <- true
            timeDraggerDash.applyDelta(multiplier)
            requestRedraw()
        }
        return(true)
    }
    me timeValue: getValue() <- {return(timeDraggerDash.timeData)}
    me void: setValue(me timeValue: val) <- {}
}
